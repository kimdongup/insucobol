//KERC7127 JOB (KER),'KER00',CLASS=G,MSGCLASS=X,MSGLEVEL=(1,1)          
//STEP1 EXEC YCOBBAT,MBR=K00C7127                                       
//COB.SYSIN DD *                                                        
      *=================================*                               
       IDENTIFICATION          DIVISION.                                
       PROGRAM-ID.             K00C7127.                                
       ENVIRONMENT             DIVISION.                                
       CONFIGURATION           SECTION.                                 
       INPUT-OUTPUT            SECTION.                                 
       FILE-CONTROL.                                                    
           SELECT  FILE-IN1    ASSIGN  TO  FILEIN1.                     
           SELECT  FILE-IN2    ASSIGN  TO  FILEIN2.                     
           SELECT  FILE-OUT    ASSIGN  TO  FILEOUT.                     
       DATA                    DIVISION.                                
       FILE                    SECTION.                                 
       FD  FILE-IN1          RECORDING  MODE     IS   F                 
                             LABEL   RECORD      IS   STANDARD          
                             BLOCK   CONTAINS    50   RECORDS           
                             RECORD  CONTAINS   283   CHARACTERS.       
       01  REC-K4.   COPY   K00B9018.                                   
                                                                        
       FD  FILE-IN2          RECORDING  MODE     IS   F                 
                             LABEL   RECORD      IS   STANDARD          
                             BLOCK   CONTAINS   100   RECORDS           
                             RECORD  CONTAINS   124   CHARACTERS.       
       01  REC-K3.   COPY   K00B9017.                                   
                                                                        
       FD  FILE-OUT          RECORDING  MODE     IS   F                 
                             LABEL   RECORD      IS   STANDARD          
                             BLOCK   CONTAINS    50   RECORDS           
                             RECORD  CONTAINS   283   CHARACTERS.       
       01  REC-OUT               PIC X(283).                            
      *-------------------------------*                                 
       WORKING-STORAGE         SECTION.                                 
      *-------------------------------*                                 
       01  REC-9007.    COPY   K00B9028.                                
       01  K4-KEY.                                                      
           02  WK4-JEUNGBEON        PIC 9(7).                           
           02  WK4-BOJONG-T         PIC 9(4).                           
       01  K3-KEY.                                                      
           02  WK3-JEUNGBEON        PIC 9(7).                           
           02  WK3-BOJONG-T         PIC 9(4).                           
       01  WORK-SPACE.                                                  
         02 WK-JNGBEON              PIC 9(7).                           
             88  S-JNGBEON  VALUE                                       
     ***----------미지급소멸건---------------------------------------*
                 5002374  5027010  5040850  5052342  5093432            
                 5090327  5079101  5021460  5072056.                    
     ***----------조사중인　계약-------------------------------------*
                                                                        
         02  WK-BOJONG              PIC 9(4).                           
             88  MUBAE-BOJ     VALUE 8201 THRU 8202  8210               
9906                                 8381 8382 8391.                    
         02  BOJONG-T               PIC 9(4).                           
             88  JNG-BOJ       VALUE 6001      9001.                    
         02  I                      PIC 9(2).                           
         02  WK-YEJ-IYUL            PIC S9(5)    COMP-3.                
         02  TOT                    PIC S9(10).                         
         02  CNT-IN1                PIC 9(7).                           
         02  CNT-IN2                PIC 9(7).                           
         02  CNT-OUT                PIC 9(7).                           
         02  CNT-ERR-IN             PIC 9(7).                           
         02  CNT-MATCH              PIC 9(7).                           
       01  REC-SWITCH.                                                  
           02  FILE-EOF-SWITCH-1       PIC X(3)  VALUE  'NO '.          
               88  FILE-EOF-1                    VALUE  'YES'.          
           02  FILE-EOF-SWITCH-2       PIC X(3)  VALUE  'NO '.          
               88  FILE-EOF-2                    VALUE  'YES'.          
       EJECT                                                            
      *=================================*                               
       PROCEDURE               DIVISION.                                
      *=================================*                               
      *---------*                                                       
       000-BODY.                                                        
      *---------*                                                       
           OPEN            INPUT         FILE-IN1     FILE-IN2          
                           OUTPUT        FILE-OUT.                      
           MOVE  ZERO  TO  WORK-SPACE.                                  
           PERFORM         110-READ-FILEIN-1.                           
           PERFORM         120-READ-FILEIN-2.                           
           PERFORM         100-PG-CONTROL       UNTIL  (FILE-EOF-1      
                                                OR      FILE-EOF-2).    
           PERFORM         200-PG-CONTROL       UNTIL   FILE-EOF-1.     
           PERFORM         300-PG-CONTROL       UNTIL   FILE-EOF-2.     
           DISPLAY    '*===========================================*'.  
           DISPLAY    '      CNT-IN1   TOTAL  =  '  CNT-IN1.            
           DISPLAY    '      CNT-IN2   TOTAL  =  '  CNT-IN2.            
           DISPLAY    '      CNT-MATCH TOTAL  =  '  CNT-MATCH.          
           DISPLAY    '      CNT-ERR          =  '  CNT-ERR-IN.         
           DISPLAY    '      CNT-OUT   TOTAL  =  '  CNT-OUT.            
           DISPLAY    '*===========================================*'.  
           CLOSE           FILE-IN1   FILE-IN2  FILE-OUT.               
           STOP RUN.                                                    
       EJECT                                                            
      *---------------*                                                 
       100-PG-CONTROL.                                                  
      *---------------*                                                 
           IF  K4-KEY  =  K3-KEY                                        
                        PERFORM          130-MATCH-CHEORI               
                        PERFORM          101-MOVE-WRITE                 
                        PERFORM          110-READ-FILEIN-1              
                        PERFORM          120-READ-FILEIN-2              
           ELSE                                                         
           IF  K4-KEY   >  K3-KEY                                       
                        PERFORM          140-I2-ERR-CHEORI              
                        PERFORM          120-READ-FILEIN-2              
           ELSE                                                         
                        PERFORM          101-MOVE-WRITE                 
                        PERFORM          110-READ-FILEIN-1.             
      *---------------*                                                 
       200-PG-CONTROL.                                                  
      *---------------*                                                 
           PERFORM          101-MOVE-WRITE.                             
           PERFORM          110-READ-FILEIN-1.                          
      *---------------*                                                 
       300-PG-CONTROL.                                                  
      *---------------*                                                 
           PERFORM          140-I2-ERR-CHEORI.                          
           PERFORM          120-READ-FILEIN-2.                          
      *------------------*                                              
       110-READ-FILEIN-1.                                               
      *------------------*                                              
           READ    FILE-IN1  END   MOVE  'YES'  TO  FILE-EOF-SWITCH-1.  
           IF  NOT FILE-EOF-1    ADD     1           TO  CNT-IN1        
                                 MOVE  K4-JEUNGBEON  TO  WK4-JEUNGBEON  
                                 MOVE  K4-BOJONG-T   TO  WK4-BOJONG-T.  
      *-----------------*                                               
       120-READ-FILEIN-2.                                               
      *-----------------*                                               
           READ  FILE-IN2    END   MOVE  'YES'  TO  FILE-EOF-SWITCH-2.  
           IF  NOT  FILE-EOF-2   ADD     1           TO  CNT-IN2        
                                 MOVE  K3-JEUNGBEON  TO  WK3-JEUNGBEON  
                                 MOVE  K3-BOJONG-T   TO  WK3-BOJONG-T.  
      *----------------*                                                
       130-MATCH-CHEORI.                                                
      *----------------*                                                
           IF   K3-BOJONG-T  =  9510  OR  9621                          
                MOVE   K3-NABIB-HOISU          TO   K4-LNAB-HOISU.      
           ADD    K3-PURE-YY                   TO   K4-PURE-YY.         
           ADD    K3-PURE-MM    K3-SEON-IJA    TO   K4-PURE-MM.         
           ADD    K3-HAIYAG-YY                 TO   K4-HAIYAG-YY.       
           ADD    K3-HAIYAG-MM  K3-SEON-IJA    TO   K4-HAIYAG-MM.       
           MOVE   K3-IYUL                      TO   K4-IYUL.            
           COMPUTE   TOT  =    K4-HAIYAG-YY    +    K4-HAIYAG-MM.       
           IF  TOT  <  ZERO                                             
                  MOVE  ZERO   TO    K4-HAIYAG-YY   K4-HAIYAG-MM.       
           MOVE  K4-JEUNGBEON  TO  WK-JNGBEON.                          
           IF  S-JNGBEON                                                
              IF  K4-BSMG = 4                                           
                     DISPLAY    'JOSA  NEED! = ' K4-JEUNGBEON           
                                    ' BSM= ' K4-BSMG                    
              ELSE   MOVE  ZERO     TO  K4-PURE-YY    K4-PURE-MM        
                                        K4-HAIYAG-YY  K4-HAIYAG-MM      
                                        K4-PB         K4-MI-SEON-GEUM   
                                        K4-SEON-HOISU.                  
           ADD    1    TO      CNT-MATCH.                               
      *-----------------*                                               
       140-I2-ERR-CHEORI.                                               
      *-----------------*                                               
           ADD    1    TO      CNT-ERR-IN.                              
           DISPLAY   '  ERROR  KEY  ==>   '  K3-KEY '  ' K4-KEY.        
      *----------------*                                                
       150-IJA-COMPUTE.                                                 
      *----------------*                                                
           MOVE      K4-BOJONG         TO      WK-BOJONG.               
           MOVE      K4-JEUNGBEON      TO      KI-JEUNGBEON.            
           MOVE      K4-BOJONG         TO      KI-BOJONG.               
           MOVE      K4-BSMG           TO      KI-BSMG.                 
           MOVE      K4-GYE-YMD       TO      KI-GYE-YMD.               
           MOVE      K4-NABGI          TO      KI-NABGI.                
           MOVE      K4-MANGI          TO      KI-MANGI.                
           MOVE      K4-NABBANG        TO      KI-NABBANG.              
           MOVE      K4-LNAB-YM       TO      KI-LNAB-YM.               
           MOVE      K4-BOGEUM         TO      KI-BOGEUM.               
           MOVE      K4-BOLYO          TO      KI-BOLYO.                
           MOVE      K4-JIGB-YMD      TO      KI-JIGB-YMD.              
           IF  K4-BSMG = 7 OR 8      MOVE   7   TO  K4-BSMG.            
           COMPUTE KI-JUNBI-GM ROUNDED =   K4-HAIYAG-YY  + K4-HAIYAG-MM.
           IF  MUBAE-BOJ                                                
9906         IF  K4-GYE-YMD > 19990331  MOVE  750  TO  WK-YEJ-IYUL      
             ELSE                                                       
               IF     K4-MANGI < 11      MOVE  950  TO  WK-YEJ-IYUL     
               ELSE                      MOVE  850  TO  WK-YEJ-IYUL     
           ELSE                                                         
9906         IF  K4-GYE-YMD > 19990331  MOVE  650  TO  WK-YEJ-IYUL      
             ELSE                        MOVE  750  TO  WK-YEJ-IYUL.    
           DIVIDE  WK-YEJ-IYUL  BY  10000    GIVING  KI-IYUL.           
           CALL    'K11M8023'   USING    REC-9007.                      
           IF   KI-CODE   NOT = ZERO                                    
                  DISPLAY  'ERR-SUB = ' KI-CODE ' JNG = ' K4-JEUNGBEON  
                           ' NB = '  K4-NABBANG  ' BOJ = ' K4-BOJONG.   
           MOVE    KI-IJA               TO     K4-IJA.                  
      *--------------*                                                  
       101-MOVE-WRITE.                                                  
      *--------------*                                                  
           MOVE    ZERO      TO     K4-IJA.                             
           IF  (K4-BSMG = 3 OR 7 OR 8)  PERFORM  150-IJA-COMPUTE.       
                                                                        
           WRITE      REC-OUT       FROM    REC-K4.                     
           ADD           1          TO      CNT-OUT.                    
